TopRule = $* 
            $WEATHER{domain='weather'}
                    {intent=WEATHER._intent}
                    {country=WEATHER._country}
                    {state=WEATHER._state}
                    {city=WEATHER._city}
                    {date=WEATHER._date}
                    {time=WEATHER._time}
          $*
;

### WEATHER DOMAIN ###
WEATHER =
    $INTENT {_intent=INTENT._intent}{_country='null'}{_state='null'}{_city='null'}{_date='null'}{_time='null'}
    (
        ?( $*
            ($LOCATION{_country=LOCATION._country}{_state=LOCATION._state}{_city=LOCATION._city} 
                ?($* $TIMEFRAME{_date=TIMEFRAME._date}{_time=TIMEFRAME._time})
            ) |
            ($TIMEFRAME{_date=TIMEFRAME._date}{_time=TIMEFRAME._time}
                ? ($* $LOCATION{_country=LOCATION._country}{_state=LOCATION._state}{_city=LOCATION._city})
            )
         )
    )
; 
   
### LOCATION FACTORY SLOTS ###
LOCATION = 
    $USCITYSTATE {_country=USCITYSTATE._country}{_state=USCITYSTATE._state}{_city=USCITYSTATE._city} |
    $USSTATE {_country=USSTATE._country}{_state=USSTATE._state}{_city=USSTATE._city} #|
    #$COUNTRY {_country=COUNTRY._country}{_state=COUNTRY._state}{_city=COUNTRY._city} |
    #$WORDCITY {_country=WORLDCITY._country}{_state=WORLDCITY._state}{_city=WORLDCITY._city} 
;


USCITYSTATE = $factory:city_state {_country='null'}{_state = city_state._state}{_city = city_state._city}
              ?($* $USA){_country='usa'}
;

USSTATE = $factory:state {_country='null'}{_state=state._nl}{_city='null'}
          ?($* $USA){_country='usa'}
;

#COUNTRY = $factory:country {_country=country._country}{_state='null'}{_city='null'};

#WORLDCITY = $factory:world_city {_country=world_city._country}{_state='null'}{_city=world_city._city};

### TIME ### 
TIMEFRAME = 
   ( $factory:date {_date=date._date_nl}{_time=date._time_nl} ) |
   ( $factory:time {_date='null'}{_time=time._time_nl} )
; 

### INTENTS ###
INTENT = (
	$WEATHER_ALONE {_intent='weather'} |
	$WEATHER_TEMP {_intent='weatherTemp'} |
	$WEATHER_HIGH_TEMP {_intent='weatherTempHigh'} |
	$WEATHER_LOW_TEMP {_intent='weatherTempLow'} |
	$WEATHER_WIND {_intent='weatherWind'} |
	$WEATHER_HUMIDITY {_intent='weatherHumidity'} |
	$WEATHER_RAIN {_intent='weatherRain'} |
	$WEATHER_SNOW {_intent='weatherSnow'} |
	$WEATHER_RAIN_STOP {_intent='weatherRainStop'} |
	$WEATHER_RAIN_START {_intent='weatherRainStart'} |
	$WEATHER_RAIN_UMBRELLA {_intent='weatherRainUmbrella'} |
	$WEATHER_SUNSET {_intent='weatherSunset'} |
	$WEATHER_SUNRISE {_intent='weatherSunrise'}
);

WEATHER_ALONE = ?($WHATS $*) $WEATHER_W;
WEATHER_TEMP =  ?($WHATS $*) $TEMP;
WEATHER_HIGH_TEMP = ?($WHATS $*) $HIGHTEMP;
WEATHER_LOW_TEMP = ?($WHATS $*) $LOWTEMP;
WEATHER_WIND =  ?($HOW | $WHATS | $ISIT $*) $WIND;
WEATHER_HUMIDITY = ?($HOW | $WHATS | $ISIT $*) $HUMIDITY;
WEATHER_RAIN = ?($HOW | $WHATS | $ISIT $*) $RAIN;
WEATHER_SNOW = ?($HOW | $WHATS | $ISIT $*) $SNOW;
WEATHER_RAIN_STOP = ?($WHEN $*) ($STOP  $* $RAIN)|($RAIN  $* $STOP);
WEATHER_RAIN_START = ?($WHEN $*) ($START  $* $RAIN)|($RAIN  $* $START);
WEATHER_RAIN_UMBRELLA = $BRING $* $RAINPROP;
WEATHER_SUNSET = ?($WHEN $*) $SUNSET;
WEATHER_SUNRISE = ?($WHEN $*) $SUNRISE;
USA = (united states ?(?of america)) | (u s a)| (u. s. a.) | (usa);

### USEFUL VOCABULARY ###
RAIN = rain | raining | rains | pouring ;
STOP = stop | stops | stopping | end | ends | ending | quit | quitting | (be over) | (is over);
START = (start | starts | starting | begin | beginning);
RAINPROP = [umbrella?s] | [raincoat?s] | (rain [coat?s]) ;
WHATS = ( what is) | (what\'s) | (tell me) ;
HOW = how | (how\'s) | (how is) ;
WHEN =  (when\'s) | (?at what time ?is) | (when ?is)  ;
SNOW = snow | snowing ;
SUNRISE = sunrise | (sun rise) | (sun come up) ;
SUNSET = (sunset | (sun set) | (sun (go|goes) down)) ;
TEMP = temp | temperature ;
HIGHTEMP = high $TEMP; 
LOWTEMP = low $TEMP;
WEATHER_W = weather;
WIND = (windy | wind) ?speed;
HUMIDITY = (humid | humidity);
ISIT = (is it)|(is there)|(are there)|(will it);
BRING = (need | have | bring | wear | carry | take);

